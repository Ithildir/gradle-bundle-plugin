buildscript {
    repositories {
        mavenCentral()

        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'nexus'

group = 'org.dm.gradle'
version = '0.4'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()

    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
    }
}

[compileJava, compileTestJava, compileIntegTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'biz.aQute.bnd:bndlib:2.3.0'

    if (project.getGradle().gradleVersion.startsWith('2'))
        testCompile 'org.spockframework:spock-core:1.0-groovy-2.3-SNAPSHOT'
    else
        testCompile 'org.spockframework:spock-core:1.0-groovy-1.8-SNAPSHOT'

    testRuntime 'cglib:cglib-nodep:3.1'

    integTestCompile sourceSets.main.output
    integTestCompile configurations.testCompile
    integTestRuntime configurations.testRuntime
}

task integTest(type: Test, dependsOn: classes) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    reports.html.destination = file("$buildDir/reports/integTests")
    reports.junitXml.destination = file("$buildDir/integTest-results")
    binResultsDir = file("${reports.junitXml.destination}/binary/integTest")
    systemProperty "gradle.home", project.getGradle().getGradleHomeDir()
}

check.dependsOn integTest


modifyPom {
    project {
        name 'Gradle Bundle Plugin'
        packaging 'jar'
        description 'Gradle plugin to generate OSGI bundles.'
        url 'https://github.com/TomDmitriev/gradle-bundle-plugin'
        inceptionYear '2014'

        scm {
            url 'scm:https://github.com/TomDmitriev/gradle-bundle-plugin.git'
            connection 'scm:https://github.com/TomDmitriev/gradle-bundle-plugin.git'
            developerConnection 'scm:https://github.com/TomDmitriev/gradle-bundle-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'TomDmitriev'
                name 'Artyom Dmitriev'
            }
        }
    }
}

eclipse.classpath.file {
    whenMerged {  cp ->
        cp.entries.removeAll { (it.kind == "src") && (it.path =~ /resources/) }
    }
}
