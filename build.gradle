buildscript {
    repositories {
        mavenCentral()

        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
    }
}

apply plugin: 'groovy'
apply plugin: 'nexus'

group = 'org.dm.gradle'
version = '0.2'

repositories {
    mavenCentral()

    maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'biz.aQute.bnd:bndlib:2.2.0'

    testCompile 'org.spockframework:spock-core:1.0-groovy-1.8-SNAPSHOT'
    testRuntime 'cglib:cglib-nodep:3.1'

    integTestCompile sourceSets.main.output
    integTestCompile configurations.testCompile
    integTestRuntime configurations.testRuntime
}

task integTest(type: Test, dependsOn: classes) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    reports.html.destination = file("$buildDir/reports/integTests")
    reports.junitXml.destination = file("$buildDir/integTest-results")
    binResultsDir = file("${reports.junitXml.destination}/binary/integTest")
}

check.dependsOn integTest


modifyPom {
    project {
        name 'Gradle Bundle Plugin'
        packaging 'jar'
        description 'Gradle plugin to generate OSGI bundles.'
        url 'https://github.com/TomDmitriev/gradle-bundle-plugin'
        inceptionYear '2014'

        scm {
            url 'scm:https://github.com/TomDmitriev/gradle-bundle-plugin.git'
            connection 'scm:https://github.com/TomDmitriev/gradle-bundle-plugin.git'
            developerConnection 'scm:https://github.com/TomDmitriev/gradle-bundle-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'TomDmitriev'
                name 'Artyom Dmitriev'
            }
        }
    }
}
